
Процедура ПечатьРаспределниеНаПродукцию(ТабДок, Ссылка) Экспорт
    
    // получим макет и области
    Макет = Документы.РаспределениеПрочихЗатрат.ПолучитьМакет("ПечатьРаспределниеНаПродукцию");
    ОбластьЗаголовок                = Макет.ПолучитьОбласть("Заголовок");
    Шапка                           = Макет.ПолучитьОбласть("Шапка");
    Область                         = Макет.ПолучитьОбласть("СуммыКРаспределениюШапка");
    ОбластьСуммыКРаспределению      = Макет.ПолучитьОбласть("СуммыКРаспределению");
    ОбластьИтогиСуммыКРаспределению = Макет.ПолучитьОбласть("СуммыКРаспределениюИтоги");
    ОбластьЗатратыНаПродукциюШапка  = Макет.ПолучитьОбласть("ЗатратыНаПродукциюШапка");
    ОбластьЗатратыНаПродукцию       = Макет.ПолучитьОбласть("ЗатратыНаПродукцию");
    ОбластьИтогиСуммыЗатраты        = Макет.ПолучитьОбласть("ЗатратыКРаспределениюИтоги");
    
    // получим выборку
    Выборка = ПолучитьВыборку(Ссылка);
    
    // выведем выборку в табличный документ
    ТабДок.Очистить();
    ВставлятьРазделительСтраниц = Ложь;
    Пока Выборка.Следующий() Цикл
        
        Если ВставлятьРазделительСтраниц Тогда
            ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
        КонецЕсли;
        
        ТабДок.Вывести(ОбластьЗаголовок);
        
        Шапка.Параметры.Заполнить(Выборка);
        ТабДок.Вывести(Шапка, Выборка.Уровень());
        
        // ПЕРВАЯ ТЧ ///////////////////////
        ТабДок.Вывести(Область);
        ВыборкаСуммыКРаспределению = Выборка.СуммыКРаспределению.Выбрать();
        Пока ВыборкаСуммыКРаспределению.Следующий() Цикл
            ОбластьСуммыКРаспределению.Параметры.Заполнить(ВыборкаСуммыКРаспределению);
            ТабДок.Вывести(ОбластьСуммыКРаспределению);
        КонецЦикла;
        // итоги первой тч
        ОбластьИтогиСуммыКРаспределению.Параметры.СуммаИтогоСуммы = Выборка.ИтогоСуммаКРаспределению;
        ТабДок.Вывести(ОбластьИтогиСуммыКРаспределению);
               
        // ВТОРАЯ ТЧ ///////////////////////
        ТабДок.Вывести(ОбластьЗатратыНаПродукциюШапка);
        ВыборкаЗатратыНаПродукцию = Выборка.ЗатратыНаПродукцию.Выбрать();
        Пока ВыборкаЗатратыНаПродукцию.Следующий() Цикл
            ОбластьЗатратыНаПродукцию.Параметры.Заполнить(ВыборкаЗатратыНаПродукцию);
            ТабДок.Вывести(ОбластьЗатратыНаПродукцию, ВыборкаЗатратыНаПродукцию.Уровень());
        КонецЦикла;
        // итоги второй тч
        ОбластьИтогиСуммыЗатраты.Параметры.СуммаИтогоЗатраты = Выборка.ИтогоСуммаЗатратНаПродукцию;;
        ТабДок.Вывести(ОбластьИтогиСуммыЗатраты);
        
        ВставлятьРазделительСтраниц = Истина;		
    КонецЦикла;

КонецПроцедуры

Функция ПолучитьВыборку(МассивСсылок)

    Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
    |   РаспределениеПрочихЗатрат.Дата,
    |   РаспределениеПрочихЗатрат.Номер,
    |   РаспределениеПрочихЗатрат.Организация,
    |   РаспределениеПрочихЗатрат.Ссылка КАК Ссылка,
    |   РаспределениеПрочихЗатрат.СхемаРаспределения,
    |   РаспределениеПрочихЗатрат.СхемаРаспределения.СчетЗатрат,
    |   РаспределениеПрочихЗатрат.СуммыКРаспределению.(
    |       Субконто1,
    |       Субконто2,
    |       Субконто3,
    |       СУММА(Сумма),
    |       СчетЗатрат
    |   ),
    |   РаспределениеПрочихЗатрат.ЗатратыНаПродукцию.(
    |       Спецификация,
    |       СтатьяЗатрат,
    |       СУММА(Сумма),
    |       СчетЗатрат
    |   ),
    |   ВложенныйЗапрос.ИтогоСуммаЗатратНаПродукцию,
    |   ВложенныйЗапрос1.ИтогоСуммаКРаспределению
    |ИЗ
    |   Документ.РаспределениеПрочихЗатрат КАК РаспределениеПрочихЗатрат
    |       ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
    |           РаспределениеПрочихЗатратЗатратыНаПродукцию.Ссылка КАК Ссылка,
    |           СУММА(РаспределениеПрочихЗатратЗатратыНаПродукцию.Сумма) КАК ИтогоСуммаЗатратНаПродукцию
    |       ИЗ
    |           Документ.РаспределениеПрочихЗатрат.ЗатратыНаПродукцию КАК РаспределениеПрочихЗатратЗатратыНаПродукцию
    |       ГДЕ
    |           РаспределениеПрочихЗатратЗатратыНаПродукцию.Ссылка В(&МассивСсылок)
    |       
    |       СГРУППИРОВАТЬ ПО
    |           РаспределениеПрочихЗатратЗатратыНаПродукцию.Ссылка) КАК ВложенныйЗапрос
    |       ПО РаспределениеПрочихЗатрат.Ссылка = ВложенныйЗапрос.Ссылка
    |       ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
    |           РаспределениеПрочихЗатратСуммыКРаспределению.Ссылка КАК Ссылка,
    |           СУММА(РаспределениеПрочихЗатратСуммыКРаспределению.Сумма) КАК ИтогоСуммаКРаспределению
    |       ИЗ
    |           Документ.РаспределениеПрочихЗатрат.СуммыКРаспределению КАК РаспределениеПрочихЗатратСуммыКРаспределению
    |       ГДЕ
    |           РаспределениеПрочихЗатратСуммыКРаспределению.Ссылка В(&МассивСсылок)
    |       
    |       СГРУППИРОВАТЬ ПО
    |           РаспределениеПрочихЗатратСуммыКРаспределению.Ссылка) КАК ВложенныйЗапрос1
    |       ПО РаспределениеПрочихЗатрат.Ссылка = ВложенныйЗапрос1.Ссылка
    |ГДЕ
    |   РаспределениеПрочихЗатрат.Ссылка В(&Массивссылок)
    |
    |СГРУППИРОВАТЬ ПО
    |   РаспределениеПрочихЗатрат.СуммыКРаспределению.(СчетЗатрат,
    |   Субконто1,
    |   Субконто2,
    |   Субконто3),
    |   РаспределениеПрочихЗатрат.ЗатратыНаПродукцию.(Спецификация,
    |   СтатьяЗатрат,
    |   СчетЗатрат)
    |ИТОГИ ПО
    |   ОБЩИЕ,
    |   Ссылка";
    Запрос.УстановитьПараметр("Массивссылок", Массивссылок);
    Выборка = Запрос.Выполнить().Выбрать();
    
    Возврат Выборка;

КонецФункции
